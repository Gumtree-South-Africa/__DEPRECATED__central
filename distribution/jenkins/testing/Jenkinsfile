#!/usr/bin/env groovy

library "commonFunctions@${ghprbSourceBranch}"

runBuild = { param ->
    echo "Running $param"
    commonFunctions.forcefullyRemoveStaleDockerContainers()
    ansiColor('xterm') {
        sh "cd ecg-comaas-central; bin/build.sh $param"
    }
}

startComaasForTenant = { tenant ->
    sh "mkdir -p /tmp/mailreceiver"

    String currentTime = sh(
            script: "date +'%s'",
            returnStdout: true
    ).trim()
    String comaasDockerName = "comaasztestz${tenant}z${currentTime}"

    try {
        commonFunctions.forcefullyRemoveStaleDockerContainers()

        commonFunctions.downloadComaasDocker()

        ansiColor('xterm') {
            sh "cd ecg-comaas-docker; ECG_COMAAS_DOCKER_PROJECT_NAME=$comaasDockerName make up"
        }

        echo "Starting Comaas for tenant $tenant"
        sh "cd ecg-comaas-central; bin/wiring-test.sh $tenant $comaasDockerName"
    } finally {
        if (commonFunctions.isDirPresent(env.WORKSPACE as String, 'ecg-comaas-docker') && comaasDockerName != null && comaasDockerName != '') {
            ansiColor('xterm') {
                sh "cd ecg-comaas-docker; ECG_COMAAS_DOCKER_PROJECT_NAME=$comaasDockerName make down"
            }
        }
    }
}

void setEnvVars() {
    env.MAVEN_HOME = "/usr/share/apache-maven"
    env.PATH = "$env.MAVEN_HOME/bin:$env.PATH"
}

node('build') {
    checkout scm

    configFile = readYaml file: "distribution/jenkins/testing/configuration.yaml"

    setEnvVars()

    tenants = commonFunctions.activeTenants(configFile.tenants.disabled as List)

    buildSteps = ["failFast": false]

    buildSteps += ["unit-tests": commonFunctions.wrapWithStepDefinition("-t", runBuild, ghprbActualCommit as String, "Unit tests")]
    buildSteps += ["integration-tests": commonFunctions.wrapWithStepDefinition("-i", runBuild, ghprbActualCommit as String, "Integration tests")]

    wiringTenants = commonFunctions.activeTenants(configFile.tenants.wiring_disabled as List)
    buildSteps += wiringTenants.collectEntries { tenant ->
        [((String) "wiring-test-$tenant"): commonFunctions.wrapWithStepDefinition(tenant, startComaasForTenant, ghprbActualCommit as String, "Start Comaas for $tenant")]
    }

    parallel buildSteps

    commonFunctions.sendToSlackJenkinsChannel('#00FF00', "Testing <${ghprbPullLink}|pull request ${ghprbPullId}> succeeded (<${BUILD_URL}|Build>)")
}
