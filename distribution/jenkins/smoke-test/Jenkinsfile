#!/usr/bin/env groovy

library "commonFunctions@${PROVIDED_PIPELINE_BRANCH}"

static String getSmtpAddress(final String dc, final String tenant, final String envShort) {
    return "${dc}.smtp-${tenant}.${envShort}.comaas.cloud"
}

static String getApiHost(final String dc, final String tenant, final String envShort) {
    return "${dc}.${tenant}.${envShort}.comaas.cloud:80"
}

def runSmokeTests(final String dc, final String tenant, final String envShort, final String smokeTestVersion, final String runPostMessageTests) {
    commonFunctions.loginToDocker()

    final String apiHost = getApiHost(dc, tenant, envShort)
    final String smtpAddress = getSmtpAddress(dc, tenant, envShort)
    final String postMsgParameter = ""
    if ( runPostMessageTests == "true" ) {
        postMsgParameter="-run_post_message_tests"
    }
    sh "docker pull dock.es.ecg.tools/comaas/smoke-tests:${smokeTestVersion}"
    sh "docker run --rm --network=host dock.es.ecg.tools/comaas/smoke-tests:${smokeTestVersion} -api_host=${apiHost} -smtp_host=${smtpAddress} -tenant ${tenant} ${postMsgParameter}"
}

Closure wrapWithStepDefinition(final String tenantLong, final String environmentShort,
                               final String dc, final String stageName, final String smokeTestVersion,
                               final String runPostMessageTests) {
    return {
        stage(stageName) {
            node('qa') {
                try {
                    final String tenantShort = commonFunctions.getAlias(tenantLong)
                    switch (dc) {
                        case 'both':
                            runSmokeTests("ams1", tenantShort, environmentShort, smokeTestVersion, runPostMessageTests)
                            runSmokeTests("dus1", tenantShort, environmentShort, smokeTestVersion, runPostMessageTests)
                            break
                        case 'active':
                            runSmokeTests(commonFunctions.getActiveDC(tenantLong, environmentShort), tenantShort, environmentShort, smokeTestVersion, runPostMessageTests)
                            break
                        case 'passive':
                            runSmokeTests(commonFunctions.getPassiveDcProd(tenantLong), tenantShort, environmentShort, smokeTestVersion, runPostMessageTests)
                            break
                        default:
                            runSmokeTests(dc, tenantShort, environmentShort, smokeTestVersion, runPostMessageTests)
                            break
                    }
                } catch (ex) {
                    echo "--->>> Exception: $ex"
                    String exMessage = ex.getMessage()
                    String slackMessage
                    slackMessage = "Smoke tests against Comaas `$tenantLong $environmentShort $dc` failed with `$exMessage` (<${BUILD_URL}|Build>)"
                    if (environmentShort == "prod") {
                        commonFunctions.sendSlackNotification('#FF0000', tenantLong, slackMessage)
                    } else {
                        commonFunctions.sendToSlackJenkinsChannel('#FF0000', slackMessage)
                    }
                    error(ex.getMessage())
                }
            }
        }
    }
}

node('lp') {
    checkout scm

    configFile = readYaml file: "distribution/jenkins/smoke-test/configuration.yaml"

    String[] tenants
    if (PROVIDED_TENANT == 'all-enabled') {
        tenants = commonFunctions.activeTenants(configFile.tenants.disabled as List)
    } else {
        tenants = [PROVIDED_TENANT]
    }

    if (PROVIDED_ENV != "prod") {
        PROVIDED_DC = "ams1"
    }

    currentBuild.description = "$PROVIDED_TENANT/$PROVIDED_DC/$PROVIDED_ENV"

    buildSteps = ["failFast": false]
    buildSteps += tenants.collectEntries { tenant ->
        version = PROVIDED_VERSION != "" ? PROVIDED_VERSION: configFile.smokeTest.version[PROVIDED_ENV][tenant]
        if (version == null) {
            version = configFile.smokeTest.version[PROVIDED_ENV]['default']
        }
        echo "version: $version"
        runPostMessageTests = configFile.smokeTest.runPostMessageTests[PROVIDED_ENV][tenant]
        if (runPostMessageTests == null) {
            runPostMessageTests = configFile.smokeTest.runPostMessageTests[PROVIDED_ENV]['default']
        }
        echo "runPostMessageTests: $runPostMessageTests"

        [((String) "$tenant-smoke-test"): wrapWithStepDefinition(tenant, PROVIDED_ENV as String,
                PROVIDED_DC as String, "smoke test $tenant", "$version", "$runPostMessageTests")]
    }
    parallel buildSteps
}
