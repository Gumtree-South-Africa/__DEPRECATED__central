#!/usr/bin/env groovy

static def tenantAliases() {
    [
            'mp'   : 'mp',
            'ebayk': 'ek',
            'it'   : 'it',
            'kjca' : 'ca',
            'mde'  : 'mo',
            'gtuk' : 'uk',
            'gtau' : 'au',
            'ar'   : 'ar',
            'mx'   : 'mx',
            'sg'   : 'sg',
            'za'   : 'za',
    ]
}

def checkInputParameters(providedEnv, providedDc, providedTenant) {
    if (providedDc == "-") {
        error("Please provide a DC")
    }
    if (providedTenant == "-") {
        error("Please provide a tenant")
    }
    if (providedDc != 'ams1' && providedEnv == 'lp') {
        error("The lp environment is only available in the ams1 dc.")
    }
}

String getActiveDC(String tenantLongName, String env) {
    if (env == 'lp') {
        return 'ams1'
    }

    final String tenant = tenantAliases().get(tenantLongName)
    String ip = sh(
            script: "dig +short ${tenant}.prod.comaas.cloud",
            returnStdout: true
    ).trim()

    echo "getActiveDC: ip = $ip"

    String dc = "???"
    if (ip.startsWith("10.32.24.")) {
        dc = "ams1"
    }
    if (ip.startsWith("10.32.56.")) {
        dc = "dus1"
    }

    echo "getActiveDC: dc = $dc"

    return dc
}

node(PROVIDED_ENV) {
    checkInputParameters(PROVIDED_ENV, PROVIDED_DC, PROVIDED_TENANT)

    final String environment = PROVIDED_ENV
    String dc = PROVIDED_DC
    final String tenant = PROVIDED_TENANT
    final String activeDC = getActiveDC(tenant, environment)
    boolean isActiveDC = activeDC == dc

    String description = "$tenant/$dc/$environment/"

    if (environment == "prod") {
        switch (dc) {
            case "active":
                dc = activeDC
                description += "active"
                break
            case "passive":
                dc = (activeDC == "ams1" ? "dus1" : "ams1")
                description += "passive"
                break
            default:
                description += (dc == activeDC ? "active" : "passive")
                break
        }
    } else {
        // Must be LP environment
        description += "-"
    }

    currentBuild.description = description

    timeout(time: 1, unit: 'MINUTES') {
        input message: "Restarting Comaas for tenant: ${tenant} in " + (isActiveDC ? "ACTIVE " : "") + "dc: ${dc} on env: ${environment}. Are you sure?"
    }

    stage("Restarting Comaas") {
        node("$environment && $dc") {
            ansiColor('xterm') {
                sh "PYTHONWARNINGS='ignore' servicecontrol.py --logdir ~ --config /etc/comaas/comaas-${tenant}.yaml --restartservice comaas-${tenant}"
            }

            final String tenantShort = tenantAliases().get(tenant)

            // Is this really necessary? Can we pass the files to the builder instead?
            checkout scm

            configFile = readYaml file: "distribution/jenkins/deployment/configuration.yaml"

            withCredentials([[$class          : 'UsernamePasswordMultiBinding', credentialsId: 'docker-registry',
                              usernameVariable: 'DOCKER_USER', passwordVariable: 'DOCKER_PASS']]) {
                final boolean live = configFile.tenants.live.contains(tenant)

                nomadFile = "core-api.nomad"
                if (live || environment == "lp") {
                    nomadFile = "core-api_ACTIVE.nomad"
                }

                final String deployJenkinsJob = env.BUILD_NUMBER

                // Note absence of force-count and api_count=3 (which will not be enforced)
                sh "levant deploy " +
                        "-log-level=debug " +
                        "-address='http://http.nomad.service.consul:4646' " +
                        "-var-file=distribution/jenkins/deployment/environments/${environment}.tf " +
                        "-var='version=${version}' " +
                        "-var='docker_password=${DOCKER_PASS}' " +
                        "-var='tenant=${tenant}' " +
                        "-var='tenant_short=${tenantShort}' " +
                        "-var='environment=${environment}' " +
                        "-var='region=${dc}' " +
                        "-var='api_count=3' " +
                        "-var='api_count_per_zone=1' " +
                        "-var='deploy_jenkins_job_nr=${deployJenkinsJob}' " +
                        "distribution/jenkins/deployment/${nomadFile}"
            }
        }
    }
}
