#!/usr/bin/env groovy
static def tenantAliases() {
    [
            'mp'   : 'mp',
            'ebayk': 'ek',
            'it'   : 'it',
            'kjca' : 'ca',
            'mde'  : 'mo',
            'gtuk' : 'uk',
            'gtau' : 'au',
            'ar'   : 'ar',
            'mx'   : 'mx',
            'sg'   : 'sg',
            'za'   : 'za',
    ]
}

def checkInputParameters(providedEnv, providedDc, providedTenant) {
    if (providedDc == "-") {
        error("Please provide a DC")
    }
    if (providedTenant == "-") {
        error("Please provide a tenant")
    }
    if (providedDc != 'ams1' && providedEnv == 'lp') {
        error("The lp environment is only available in the ams1 dc.")
    }
}

String getActiveDC(String tenantLongName, String env) {
    if (env == 'lp') {
        return 'ams1'
    }

    final String tenant = tenantAliases().get(tenantLongName)
    String ip = sh(
            script: "dig +short ${tenant}.prod.comaas.cloud",
            returnStdout: true
    ).trim()

    echo "getActiveDC: ip = $ip"

    String dc = "???"
    if (ip.startsWith("10.32.24.")) {
        dc = "ams1"
    }
    if (ip.startsWith("10.32.56.")) {
        dc = "dus1"
    }

    echo "getActiveDC: dc = $dc"

    return dc
}

String getMetaInformation(final String what, final String tenantLongName) {
    return sh(
            script: "curl -sL http://http.nomad.service.consul:4646/v1/job/${tenantLongName}-core-api | jq -r .Meta.${what}",
            returnStdout: true
    ).trim()
}

String getOriginalDeployNr(final String tenantLongName) {
    return getMetaInformation("deploy_jenkins_job_nr", tenantLongName)
}

String getOriginalVersion(final String tenantLongName) {
    return getMetaInformation("version", tenantLongName)
}

String getOriginalWantedInstances(final String tenantLongName) {
    return getMetaInformation("wanted_instances", tenantLongName)
}

String getOriginalWantedInstancesPerZone(final String tenantLongName) {
    return getMetaInformation("wanted_instances_per_zone", tenantLongName)
}

String getOriginalRestartJenkinsJobNr(final String tenantLongName) {
    return getMetaInformation("restart_jenkins_job_nr", tenantLongName)
}

node(PROVIDED_ENV) {
    checkInputParameters(PROVIDED_ENV, PROVIDED_DC, PROVIDED_TENANT)

    final String environment = PROVIDED_ENV
    String dc = PROVIDED_DC
    final String tenant = PROVIDED_TENANT
    final String activeDC = getActiveDC(tenant, environment)
    boolean isActiveDC = activeDC == dc

    String description = "$tenant/$dc/$environment/"

    if (environment == "prod") {
        switch (dc) {
            case "active":
                dc = activeDC
                description += "active"
                break
            case "passive":
                dc = (activeDC == "ams1" ? "dus1" : "ams1")
                description += "passive"
                break
            default:
                description += (dc == activeDC ? "active" : "passive")
                break
        }
    } else {
        // Must be LP environment
        description += "-"
    }

    currentBuild.description = description

    timeout(time: 1, unit: 'MINUTES') {
        input message: "Restarting Comaas for tenant: ${tenant} in " + (isActiveDC ? "ACTIVE " : "") + "dc: ${dc} on env: ${environment}. Are you sure?"
    }

    final String originalDeployJenkinsJobNr = getOriginalDeployNr(tenant)
    final String version = getOriginalVersion(tenant)
    final String wantedInstances = getOriginalWantedInstances(tenant)
    final String wantedInstancesPerZone = getOriginalWantedInstancesPerZone(tenant)
    final String originalJenkinsRestartJobNr = getOriginalRestartJenkinsJobNr(tenant)
    final String restartJenkinsJob = originalJenkinsRestartJobNr == "none" ? env.BUILD_NUMBER : originalJenkinsRestartJobNr + "," + env.BUILD_NUMBER

    stage("Restarting Comaas") {
        node("$environment && $dc") {
            ansiColor('xterm') {
                sh "PYTHONWARNINGS='ignore' servicecontrol.py --logdir ~ --config /etc/comaas/comaas-${tenant}.yaml --restartservice comaas-${tenant}"
            }

            final String tenantShort = tenantAliases().get(tenant)

            // Is this really necessary? Can we pass the files to the builder instead?
            checkout scm

            configFile = readYaml file: "distribution/jenkins/deployment/configuration.yaml"

            withCredentials([[$class          : 'UsernamePasswordMultiBinding', credentialsId: 'docker-registry',
                              usernameVariable: 'DOCKER_USER', passwordVariable: 'DOCKER_PASS']]) {
                final boolean live = configFile.tenants.live.contains(tenant)

                def nomadFile = "core-api.nomad"
                if (live || environment == "lp") {
                    nomadFile = "core-api_ACTIVE.nomad"
                }

                final String vars = "-var-file=distribution/jenkins/deployment/environments/${environment}.tf " +
                        "-var='version=${version}' " +
                        "-var='docker_password=${DOCKER_PASS}' " +
                        "-var='tenant=${tenant}' " +
                        "-var='tenant_short=${tenantShort}' " +
                        "-var='environment=${environment}' " +
                        "-var='region=${dc}' " +
                        "-var='api_count=${wantedInstances}' " +
                        "-var='api_count_per_zone=${wantedInstancesPerZone}' " +
                        "-var='deploy_jenkins_job_nr=${originalDeployJenkinsJobNr}' " +
                        "-var='restart_jenkins_job_nr=${restartJenkinsJob}' "

                sh "levant render ${vars} distribution/jenkins/deployment/${nomadFile}"
                sh "levant deploy -log-level=debug -address='http://http.nomad.service.consul:4646' ${vars} " +
                        "distribution/jenkins/deployment/${nomadFile}"
            }
        }
    }
}
