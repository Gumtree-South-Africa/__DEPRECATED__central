#!/usr/bin/env groovy
static def tenantAliases() {
    [
            'mp'   : 'mp',
            'ebayk': 'ek',
            'it'   : 'it',
            'kjca' : 'ca',
            'mde'  : 'mo',
            'gtuk' : 'uk',
            'gtau' : 'au',
            'ar'   : 'ar',
            'mx'   : 'mx',
            'sg'   : 'sg',
            'za'   : 'za',
    ]
}

def checkInputParameters(providedDc, providedTenant) {
    if (providedDc == "-") {
        error("Please provide a DC")
    }
    if (providedTenant == "-") {
        error("Please provide a tenant")
    }
}

String getActiveDC(String tenantShort, String env) {
    if (env == 'lp') {
        return 'ams1'
    }

    String ip = sh(
            script: "dig +short ${tenantShort}.prod.comaas.cloud",
            returnStdout: true
    ).trim()

    echo "getActiveDC: ip = $ip"

    String dc = "???"
    if (ip.startsWith("10.32.24.")) {
        dc = "ams1"
    }
    if (ip.startsWith("10.32.56.")) {
        dc = "dus1"
    }

    echo "getActiveDC: dc = $dc"

    return dc
}

String getMetaInformation(final String what, final String tenantShort) {
    return sh(
            script: "curl -sL http://http.nomad.service.consul:4646/v1/job/comaas-${tenantShort} | jq -r .Meta.${what}",
            returnStdout: true
    ).trim()
}

String getOriginalDeployNr(final String tenantShort) {
    return getMetaInformation("deploy_jenkins_job_nr", tenantShort)
}

String getOriginalVersion(final String tenantShort) {
    return getMetaInformation("version", tenantShort)
}

String getOriginalWantedInstancesHttp(final String tenantShort) {
    return getMetaInformation("wanted_instances_api", tenantShort)
}

String getOriginalWantedInstancesNewMsg(final String tenantShort) {
    return getMetaInformation("wanted_instances_newmsg", tenantShort)
}

String getOriginalRestartJenkinsJobNr(final String tenantShort) {
    return getMetaInformation("restart_jenkins_job_nr", tenantShort)
}

node("master") {
    checkInputParameters(PROVIDED_DC, PROVIDED_TENANT)

    final String environment = PROVIDED_ENV
    String dc = PROVIDED_DC
    final String tenantLong = PROVIDED_TENANT
    final String tenantShort = tenantAliases().get(tenantLong)
    final String activeDC = getActiveDC(tenantShort, environment)
    boolean isActiveDC = activeDC == dc

    String description = "$tenantLong/$dc/$environment/"

    if (environment == "prod") {
        switch (dc) {
            case "active":
                dc = activeDC
                description += "active"
                break
            case "passive":
                dc = (activeDC == "ams1" ? "dus1" : "ams1")
                description += "passive"
                break
            default:
                description += (dc == activeDC ? "active" : "passive")
                break
        }
    } else {
        // Must be LP environment
        description += "-"
        dc = "ams1"
    }

    currentBuild.description = description

    echo "Restarting Comaas for tenant: ${tenantLong} in " + (isActiveDC ? "ACTIVE" : "") + " dc: ${dc} on env: ${environment}."

    stage("Restarting Comaas") {
        node("$environment && $dc") {

            final String originalDeployJenkinsJobNr = getOriginalDeployNr(tenantShort)
            final String version = getOriginalVersion(tenantShort)
            final String wantedInstancesHttp = getOriginalWantedInstancesHttp(tenantShort)
            final String wantedInstancesNewMsg = getOriginalWantedInstancesNewMsg(tenantShort)
            final String originalJenkinsRestartJobNr = getOriginalRestartJenkinsJobNr(tenantShort)
            final String restartJenkinsJob = originalJenkinsRestartJobNr == "none" ? env.BUILD_NUMBER : originalJenkinsRestartJobNr + "," + env.BUILD_NUMBER

            parallel core: {
                ansiColor('xterm') {
                    sh "PYTHONWARNINGS='ignore' servicecontrol.py --logdir ~ --config /etc/comaas/comaas-${tenantLong}.yaml --restartservice comaas-${tenantLong}"
                }
            },
            nomad: {
                // Is this really necessary? Can we pass the files to the builder instead?
                checkout scm

                def secrets = [
                        [
                                $class: 'VaultSecret',
                                path: 'secret/nomad/dock.es.ecg.tools/comaas+robot_upload',
                                secretValues: [
                                        [$class: 'VaultSecretValue', envVar: 'DOCKER_USERNAME', vaultKey: 'username'],
                                        [$class: 'VaultSecretValue', envVar: 'DOCKER_PASSWORD', vaultKey: 'password']
                                ],
                        ],
                ]
                wrap([$class: 'VaultBuildWrapper', vaultSecrets: secrets]) {
                    // Remove after https://jira.corp.ebay.com/browse/COMAAS-950 is fixed
                    urlprefixes = ""
                    if (tenantShort == 'au') {
                        if (environment == 'lp') {
                            urlprefixes += ",\"urlprefix-au-comaas.dro.ops.qa.au.ecg.so:8006/\""
                        }
                        if (environment == 'prod') {
                            urlprefixes += ",\"urlprefix-au-rts2-api:8080/\",\"urlprefix-au-rts2-api.intern.gumtree.com.au:8080/\""
                        }
                    }

                    final String vars = "-var-file=/deployment/environments/${environment}.tf " +
                            "-var='version=${version}' " +
                            "-var='urlprefixes=${urlprefixes}' " +
                            "-var='docker_username=${DOCKER_USERNAME}' " +
                            "-var='docker_password=${DOCKER_PASSWORD}' " +
                            "-var='tenant=${tenantLong}' " +
                            "-var='tenant_short=${tenantShort}' " +
                            "-var='environment=${environment}' " +
                            "-var='region=${dc}' " +
                            "-var='api_count=${wantedInstancesHttp}' " +
                            "-var='newmsg_count=${wantedInstancesNewMsg}' " +
                            "-var='deploy_jenkins_job_nr=${originalDeployJenkinsJobNr}' " +
                            "-var='restart_jenkins_job_nr=${restartJenkinsJob}' "
                    final String levantCmd = "docker run --network host --rm -v \$(pwd)/distribution/jenkins/deployment/:/deployment:ro jrasell/levant:0.0.4 levant"

                    sh "$levantCmd render ${vars} /deployment/comaas.nomad"
                    sh "$levantCmd deploy ${vars} -log-level=debug -address='http://http.nomad.service.consul:4646' /deployment/comaas.nomad"
                }
            }
        }
    }
}
