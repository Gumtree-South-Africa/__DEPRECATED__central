#!/usr/bin/env groovy
import java.text.SimpleDateFormat

library "commonFunctions@${PROVIDED_PIPELINE_BRANCH}"

void packageComaas(String tenant, String version) {
    echo "Packaging Comaas for $tenant"
    sh "cd ecg-comaas-central && " +
            "mvn --settings etc/settings.xml versions:set -DnewVersion=\"$version\" -DgenerateBackupPoms=false &&" +
            "bin/build.sh -T ${tenant} -P prod && " +
            "mkdir -p builds && " +
            "mv -v distribution/target/*.tar.gz builds/comaas-${tenant}_${version}.tar.gz"

    echo "Creating Docker image for $tenant"
    final String dockerImageName = "dock.es.ecg.tools/comaas/comaas-${tenant}:${version}"
    final String tmpDir = "docker_image_src"
    sh "cd ecg-comaas-central && " +
            "rm -rf ${tmpDir} && " +
            "mkdir -p ${tmpDir} && " +
            "tar xf builds/comaas-${tenant}_${version}.tar.gz -C ${tmpDir} && " +
            "docker build --tag ${dockerImageName} --label tenant=${tenant} ."
    commonFunctions.loginToDocker()
    sh "docker push $dockerImageName"
    sh "docker image remove $dockerImageName"
}

def uploadToSwift(String tenant, String version, String pwd, String retentionDays) {
    echo "Uploading Comaas for $tenant with version $version to Swift (from $pwd)"

    withCredentials([[$class          : 'UsernamePasswordMultiBinding', credentialsId: 'swift',
                      usernameVariable: 'SWIFT_USER', passwordVariable: 'SWIFT_PASS']]) {
        final String file = "comaas-${tenant}_${version}.tar.gz"
        final String target = "${tenant}/builds/${file}"

        echo sh(
                script: "docker run --rm " +
                        "-v ${pwd}:/objects " +
                        "--network host " +
                        "-e OS_AUTH_URL=https://keystone.dus1.cloud.ecg.so/v2.0 " +
                        "-e OS_TENANT_NAME='comaas-qa' " +
                        "-e OS_USERNAME='$SWIFT_USER' " +
                        "-e OS_PASSWORD='$SWIFT_PASS' " +
                        "-e OS_PROJECT_NAME='comaas-control-prod' " +
                        "-e OS_REGION_NAME='dus1' " +
                        "ebayclassifiedsgroup/python-swiftclient:3.5.0 " +
                        "swift upload --header \"X-Delete-At: \$(date --date \"+${retentionDays} days\" +%s)\" --skip-identical --object-name $target comaas /objects/$file",
                returnStdout: true
        )
    }
}

// p contains [tenant, version, retentionDays]
distribute = { String[] p ->
    String tenant = p[0], version = p[1]
    String retentionDays = p[2]

    packageComaas(tenant, version)

    final String pwd = env.WORKSPACE + "/ecg-comaas-central/builds"
    uploadToSwift(tenant, version, pwd, retentionDays)
}

void setEnvVars() {
    env.MAVEN_HOME = "/usr/share/apache-maven"
    env.PATH = "$env.MAVEN_HOME/bin:$env.PATH"
}

static String createVersionString(String buildNr) {
    return new SimpleDateFormat("yyyy.MM.").format(new Date()) + buildNr
}

node('qa') {
    setEnvVars()
    final int retentionDays = 30

    PROVIDED_VERSION = PROVIDED_VERSION.trim()

    if (PROVIDED_VERSION != '' && !(PROVIDED_VERSION =~ /^\d{4}\.\d{2}\..+/)) {
        error("Provided version does not have the form 'YYYY.mm.xxx', or empty. Leave empty to create a new version on master.")
    }

    String version, gitHash
    Boolean createTag = false

    if (PROVIDED_VERSION == '') {
        gitHash = commonFunctions.getMasterSha()

        echo "Checking if commit $gitHash already has a version tag"

        commonFunctions.checkoutComaasCentralRepo(gitHash)

        version = sh(
                script: "cd ecg-comaas-central; git show-ref --dereference --tags | grep \"^$gitHash\" | sort | head -1 | sed -e 's~.* refs/tags/~~' -e 's/\\^{}//'",
                returnStdout: true
        ).trim()

        if (version != '') {
            echo "PROVIDED_VERSION was empty, but commit $gitHash already has version $version. Rebuilding but not making a new version tag"
        } else {
            version = createVersionString(env.BUILD_NUMBER as String)
            createTag = true
            echo "PROVIDED_VERSION was empty, creating new version $version on commit $gitHash"
        }
    } else {
        version = PROVIDED_VERSION
        gitHash = commonFunctions.getGitHashForTag(version)
        echo "Rebuilding version $version from commit $gitHash"
    }

    echo "Packaging version $version using git hash $gitHash"
    currentBuild.description = "$version $PROVIDED_TENANT"

    if (PROVIDED_TENANT == 'all' || PROVIDED_TENANT == '') {
        tenants = commonFunctions.allTenants()
    } else {
        tenants = [PROVIDED_TENANT]
    }

    buildSteps = ["failFast": false]
    buildSteps += tenants.collectEntries { tenant ->
        String stageName = "Package artifact for $tenant"
        [((String) "package-$tenant"): commonFunctions.wrapWithStepDefinition([tenant, version, retentionDays], distribute, gitHash, stageName)]
    }
    parallel buildSteps

    if (createTag) {
        stage("Tag commit with version") {
            node('lp') {
                commonFunctions.tagWithVersion(gitHash, version)
            }
        }
    }
}
